
BEGIN RJPSTDK

// Dakkon PIDs

IF ~IsGabber(Player1)~ THEN BEGIN DKPID
SAY @0 = @1
++ @2 EXIT
IF ~G("RJFinalVisit",1) G("RJDKFV",0)~ THEN REPLY @200 GOTO DKPID9
IF ~G("RJiniiJIDDK",6)~ THEN REPLY @10 EXTERN RJPSTDK DKPID1
IF ~G("RJiniiJIDDK",7) PartyHasItem("WA2DAK")~ THEN REPLY @61 EXTERN RJPSTDK DKPID2
IF ~G("RJiniiJIDDK",8) AreaCheck("RJ0602")~ THEN REPLY @80 GOTO DKPID3
IF ~G("RJiniiJIDDK",11)~ THEN REPLY @90 GOTO DKPID4
IF ~G("RJiniiJIDDK",99)~ THEN REPLY @95 GOTO DKPID5
END

IF ~~ THEN BEGIN DKPID3
SAY @81
++ @82 DO ~SetGlobal("RJiniiJIDDK","GLOBAL",9) MoveToPoint([2873.946])~ EXIT
END

IF ~~ THEN BEGIN DKPID4
SAY @91 = @92
++ @93 DO ~SetGlobal("RJiniiJIDDK","GLOBAL",99)~ EXIT
END

IF ~~ THEN BEGIN DKPID5
SAY @96 = @97 = @98 = @99
++ @100 EXIT
END

IF ~~ THEN BEGIN DKPID9
SAY @201 = @202
++ @203 DO ~SetGlobal("RJDKFV","GLOBAL",1)~ EXIT
END

// Dakkon 1st encounter in Ravel's maze

CHAIN IF ~~ THEN RJPSTDK DKPID1
@11 = @12 DO ~SetGlobal("RJiniiJIDDK","GLOBAL",7)~
== RJPSTDK IF ~InParty("RJRavel")~ THEN @17
== RJRV25J IF ~InParty("RJRavel")~ THEN @18
== RJPSTDK IF ~InParty("RJRavel")~ THEN @19
== RJAN25J IF ~InParty("RJAnnah")~ THEN @13 = @14
== RJGR25J IF ~!InParty("RJAnnah") InParty("RJGrace")~ THEN @13 = @14
== RJALI25J IF ~!InParty("RJAnnah") !InParty("RJGrace")~ THEN @13 = @14
== RJPSTDK @15
END
++ @16 EXTERN RJPSTDK DKPID1.1

CHAIN IF ~~ THEN RJPSTDK DKPID1.1
@20 = @21
== RJPSTDK IF ~InParty("RJGrace")~ THEN @22
== RJGR25J IF ~InParty("RJGrace")~ THEN @23
== RJPSTDK @24 = @26 = @25
END
IF ~InParty("RJRavel")~ THEN EXTERN RJRV25J DKPID1.2
IF ~!InParty("RJRavel")~ THEN EXTERN RJPSTDK DKPID1.3

CHAIN IF ~~ THEN RJRV25J DKPID1.2
@30 = @31
== RJPSTDK @32 = @33
END
+ ~!PartyHasItem("WA2DAK")~ + @34 EXTERN RJALI25J DKPID1.4
+ ~PartyHasItem("WA2DAK")~ + @35 EXTERN RJPSTDK DKPID1.5

CHAIN IF ~~ THEN RJPSTDK DKPID1.3
@40 = @41 = @42 = @33
END
+ ~!PartyHasItem("WA2DAK")~ + @34 EXTERN RJALI25J DKPID1.4
+ ~PartyHasItem("WA2DAK")~ + @35 EXTERN RJPSTDK DKPID1.5

CHAIN IF ~~ THEN RJALI25J DKPID1.4
@50 = @51 = @52 = @53
== RJFAY IF ~G("RJParkFY",0)~ THEN @56
== RJPSTDK @54
END
++ @55 DO ~AddJournalEntry(@9052,INFO)~ EXIT

CHAIN IF ~~ THEN RJPSTDK DKPID1.5
@60
END
++ @61 EXTERN RJPSTDK DKPID2

// Dakkon 2nd encounter in Ravel's maze

CHAIN IF ~~ THEN RJPSTDK DKPID2
@70 DO ~TakePartyItem("WA2DAK")~
== RJALI25J @71
== RJAN25J IF ~InParty("RJAnnah")~ THEN @72
== RJGR25J IF ~InParty("RJGrace")~ THEN @73
== RJRV25J IF ~InParty("RJRavel")~ THEN @74
== RJFAY IF ~G("RJParkFY",0)~ THEN @77
== RJPSTDK @75 DO ~MoveBetweenAreas("RJ0602",[1961.1149],4)~
END
++ @76 DO ~SetGlobal("RJiniiJIDDK","GLOBAL",8)~ EXIT
