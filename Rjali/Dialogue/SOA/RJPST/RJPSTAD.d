
BEGIN RJPSTAD

// Adahn Intro

CHAIN IF ~IsGabber(Player1) 
          G("RJAdahnMet",0) 
          G("RJiniMet",2)~ THEN RJPSTAD MeetAdahn
@0 DO ~SetGlobal("RJAdahnMet","GLOBAL",2)~
== RJINI @1
== RJPSTAD @2
END
++ @3 EXTERN RJPSTAD MA1

CHAIN IF ~~ THEN RJPSTAD MA1
@4
== RJINI @5
== RJPSTAD @6
== RJINI @7
== RJPSTAD @11
== RJFAY @12
END
++ @8 DO ~ActionOverride("RJini",MoveBetweenAreas("RJ0605",[1420.1670],8))
          ActionOverride(Player1,LeaveAreaLUA("RJ0605","",[1380.1670],8))~ EXIT

// The reunion

CHAIN IF ~G("RJiniiJIDAD",16) !G("RJPort",0)~ THEN PLAYER1 JAD4
@20 DO ~SetGlobal("RJiniiJIDAD","GLOBAL",99)~
== RJFAY @25
== RJPSTAD @26
== RJFAY @27
== RJPSTAD @28
== PLAYER1 @24 = @21
== RJPSTAD @22
END
IF ~G("RJAddCnt",0)~ THEN EXTERN PLAYER1 JAD4.0
IF ~G("RJAddCnt",1)~ THEN EXTERN PLAYER1 JAD4.1
IF ~G("RJAddCnt",2)~ THEN EXTERN PLAYER1 JAD4.2
IF ~G("RJAddCnt",3)~ THEN EXTERN PLAYER1 JAD4.3

CHAIN IF ~~ THEN PLAYER1 JAD4.0
@23 = @50
== RJPSTAD @51
== PLAYER1 @52
== RJPSTAD @53
== PLAYER1 @54
== RJALIJ @55 = @56 = @57
== RJPSTAN @58
== RJPSTGR @59
== RJPSTRV @60
== RJPSTAD @61 = @62
== RJPSTRV @63 = @64
== RJALIJ @65 = @66 = @67
== RJINI @68 = @69
== RJALIJ @70 = @71 = @72 = @73
== RJPSTRV @74
== RJPSTGR @75
== RJPSTRV @76
== RJPSTAN @77
== RJPSTAD @78 = @79 = @80
== RJALIJ @81
== RJINI @82
== RJPSTAD @83
END
IF ~~ THEN DO ~AddJournalEntry(@9041,INFO)~ EXTERN PLAYER1 JAD4.9

CHAIN IF ~~ THEN PLAYER1 JAD4.1
@23 = @50
== RJPSTAD IF ~G("RJAddRV",1)~ THEN @90 = @91 = @92
== RJPSTRV IF ~G("RJAddRV",1)~ THEN @93
== RJPSTAN IF ~G("RJAddRV",1)~ THEN @94
== RJPSTGR IF ~G("RJAddRV",1)~ THEN @95
== RJPSTRV IF ~G("RJAddRV",1)~ THEN @96
== RJPSTAD IF ~G("RJAddGR",1)~ THEN @97 = @98
== RJPSTRV IF ~G("RJAddGR",1)~ THEN @99
== RJPSTAN IF ~G("RJAddGR",1)~ THEN @100
== RJPSTRV IF ~G("RJAddGR",1)~ THEN @101
== RJPSTAD IF ~G("RJAddAN",1)~ THEN @102 = @103
== RJPSTAN IF ~G("RJAddAN",1)~ THEN @104
== RJPSTAD IF ~G("RJAddAN",1)~ THEN @105
== RJPSTAN IF ~G("RJAddAN",1)~ THEN @106
== RJPSTGR IF ~G("RJAddAN",1)~ THEN @107
== RJPSTRV IF ~G("RJAddAN",1)~ THEN @108
END
IF ~~ THEN EXTERN PLAYER1 JAD4.4

CHAIN IF ~~ THEN PLAYER1 JAD4.2
@23 = @50
== RJPSTAD IF ~!G("RJAddRV",1)~ THEN @120 = @121
== RJPSTRV IF ~!G("RJAddRV",1)~ THEN @122
== RJPSTGR IF ~!G("RJAddRV",1)~ THEN @99
== RJPSTAN IF ~!G("RJAddRV",1)~ THEN @104
== RJPSTAD IF ~!G("RJAddRV",1)~ THEN @105
== RJPSTAN IF ~!G("RJAddRV",1)~ THEN @106
== RJPSTAD IF ~!G("RJAddGR",1)~ THEN @123 = @124
== RJPSTGR IF ~!G("RJAddGR",1)~ THEN @125 = @126
== RJPSTRV IF ~!G("RJAddGR",1)~ THEN @96
== RJPSTAN IF ~!G("RJAddGR",1)~ THEN @104
== RJPSTAD IF ~!G("RJAddGR",1)~ THEN @105
== RJPSTAN IF ~!G("RJAddGR",1)~ THEN @106
== RJPSTAD IF ~!G("RJAddAN",1)~ THEN @127 = @128
== RJPSTAN IF ~!G("RJAddAN",1)~ THEN @129
== RJPSTGR IF ~!G("RJAddAN",1)~ THEN @130
== RJPSTRV IF ~!G("RJAddAN",1)~ THEN @108
END
IF ~~ THEN EXTERN PLAYER1 JAD4.4

CHAIN IF ~~ THEN PLAYER1 JAD4.3
@23 = @50
== RJPSTAD @140 = @141
== RJPSTRV @93
== RJPSTAN @104
== RJPSTAD @105
== RJPSTAN @106
== RJPSTGR @99
END
IF ~~ THEN EXTERN PLAYER1 JAD4.4

CHAIN IF ~~ THEN PLAYER1 JAD4.4
@150
== RJPSTRV @160
== RJPSTRV IF ~G("RJAddGR",1)~ THEN @161 = @162 = @163
== RJPSTRV IF ~G("RJAddAN",1)~ THEN @165 = @166 = @167
== RJPSTRV IF ~G("RJAddRV",0)~ THEN @171 = @172
== RJPSTRV IF ~G("RJAddRV",1)~ THEN @171 = @173
== RJPSTRV @168 DO ~GiveItemCreate("RJRVHA",Player1,0,0,0)~
== RJPSTRV @169 = @170 = @164 DO ~AddJournalEntry(@9046,INFO)~
END
IF ~G("RJAddCnt",0)~ THEN EXTERN PLAYER1 JAD4.8
IF ~G("RJAddAN",1) !G("RJAddLim",0)~ THEN REPLY @154 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.5.1
IF ~G("RJAddAN",1)~ THEN REPLY @180 EXTERN PLAYER1 JAD4.5.2
IF ~G("RJAddGR",1) !G("RJAddAN",1) !G("RJAddLim",0)~ THEN REPLY @157 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.6.1
IF ~G("RJAddGR",1) !G("RJAddAN",1)~ THEN REPLY @181 EXTERN PLAYER1 JAD4.6.2
IF ~G("RJAddRV",1) !G("RJAddGR",1) !G("RJAddAN",1) !G("RJAddLim",0)~ THEN REPLY @151 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.7.1
IF ~G("RJAddRV",1) !G("RJAddGR",1) !G("RJAddAN",1)~ THEN REPLY @182 EXTERN PLAYER1 JAD4.7.2

CHAIN IF ~~ THEN PLAYER1 JAD4.5.1
@155
== RJPSTAN @156 DO ~SetGlobal("RJAddAN","GLOBAL",3)~
END
IF ~!G("RJAddGR",1) !G("RJAddRV",1)~ THEN EXTERN PLAYER1 JAD4.8
IF ~G("RJAddGR",1) !G("RJAddLim",0)~ THEN REPLY @157 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.6.1
IF ~G("RJAddGR",1)~ THEN REPLY @181 EXTERN PLAYER1 JAD4.6.2
IF ~G("RJAddRV",1) !G("RJAddGR",1) !G("RJAddLim",0)~ THEN REPLY @151 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.7.1
IF ~G("RJAddRV",1) !G("RJAddGR",1)~ THEN REPLY @182 EXTERN PLAYER1 JAD4.7.2

CHAIN IF ~~ THEN PLAYER1 JAD4.5.2
@183
== RJPSTAN @184
== PLAYER1 @189 DO ~ActionOverride("RJAnnah",MoveBetweenAreas("RJ0605",[1448.1292],0))~
END
IF ~!G("RJAddGR",1) !G("RJAddRV",1)~ THEN EXTERN PLAYER1 JAD4.8
IF ~G("RJAddGR",1) !G("RJAddLim",0)~ THEN REPLY @157 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.6.1
IF ~G("RJAddGR",1)~ THEN REPLY @181 EXTERN PLAYER1 JAD4.6.2
IF ~G("RJAddRV",1) !G("RJAddGR",1) !G("RJAddLim",0)~ THEN REPLY @151 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.7.1
IF ~G("RJAddRV",1) !G("RJAddGR",1)~ THEN REPLY @182 EXTERN PLAYER1 JAD4.7.2

CHAIN IF ~~ THEN PLAYER1 JAD4.6.1
@158
== RJPSTGR @159 DO ~SetGlobal("RJAddGR","GLOBAL",3)~
END
IF ~!G("RJAddRV",1)~ THEN EXTERN PLAYER1 JAD4.8
IF ~G("RJAddRV",1) !G("RJAddLim",0)~ THEN REPLY @151 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.7.1
IF ~G("RJAddRV",1)~ THEN REPLY @182 EXTERN PLAYER1 JAD4.7.2

CHAIN IF ~~ THEN PLAYER1 JAD4.6.2
@183
== RJPSTGR @185
== PLAYER1 @189 DO ~ActionOverride("RJGrace",MoveBetweenAreas("RJ0605",[1301.1602],14))~
END
IF ~!G("RJAddRV",1)~ THEN EXTERN PLAYER1 JAD4.8
IF ~G("RJAddRV",1) !G("RJAddLim",0)~ THEN REPLY @151 DO ~IncrementGlobal("RJAddLim","GLOBAL",-1)~ EXTERN PLAYER1 JAD4.7.1
IF ~G("RJAddRV",1)~ THEN REPLY @182 EXTERN PLAYER1 JAD4.7.2

CHAIN IF ~~ THEN PLAYER1 JAD4.7.1
@158
== RJPSTRV @174 DO ~SetGlobal("RJAddRV","GLOBAL",3)~
END
IF ~~ THEN EXTERN PLAYER1 JAD4.8

CHAIN IF ~~ THEN PLAYER1 JAD4.7.2
@183
== RJPSTRV @186 = @187
== PLAYER1 @189 DO ~ActionOverride("RJRavel",MoveBetweenAreas("RJ0605",[815.1652],8))~
END
IF ~~ THEN EXTERN PLAYER1 JAD4.8

CHAIN IF ~~ THEN PLAYER1 JAD4.8
@188
== RJPSTAD @190
END
IF ~~ THEN EXTERN PLAYER1 JAD4.9

CHAIN IF ~~ THEN PLAYER1 JAD4.9
@30 = @31
== RJINI @32 = @33 = @34 DO ~ActionOverride("RJini",JumpToPoint([723.1126]))~
== PLAYER1 IF ~G("RJAddRV",3)~ THEN @35
== RJPSTRV IF ~G("RJAddRV",3)~ THEN @36 DO ~AddJournalEntry(@9036,INFO) SetGlobal("RJRavelRomanceActive","GLOBAL",1)~
== PLAYER1 IF ~G("RJAddAN",3)~ THEN @37
== RJPSTAN IF ~G("RJAddAN",3)~ THEN @38 DO ~AddJournalEntry(@9034,INFO) SetGlobal("RJAnnahRomanceActive","GLOBAL",0)~
== PLAYER1 IF ~G("RJAddGR",3)~ THEN @39
== RJPSTGR IF ~G("RJAddGR",3)~ THEN @40 DO ~AddJournalEntry(@9035,INFO) SetGlobal("RJGraceRomanceActive","GLOBAL",1)~
== PLAYER1 @41
== RJALIJ @42
== PLAYER1 @46
== PLAYER1 IF ~G("RJECTalk",1) !PartyHasItem("RJDTEARS")~ THEN @44
== PLAYER1 IF ~G("RJECTalk",1) PartyHasItem("RJDTEARS")~ THEN @45
== PLAYER1 @43
END
IF ~~ THEN EXIT

CHAIN IF ~~ THEN RJPSTAD ADDAK1
@302
== RJPSTDK @303
== RJPSTAD @304
== RJPSTDK @305
== RJPSTAD @306
== RJGR25J IF ~!InParty("RJGrace") InMyArea("RJGrace")~ THEN @307
== RJAN25J IF ~!InParty("RJAnnah") InMyArea("RJAnnah")~ THEN @308
== RJRV25J IF ~!InParty("RJRavel") InMyArea("RJRavel")~ THEN @309
== RJPSTAD IF ~!InParty("RJRavel") InMyArea("RJRavel")~ THEN @310
== RJRV25J IF ~!InParty("RJRavel") InMyArea("RJRavel")~ THEN @311
END
++ @312 EXIT

APPEND RJPSTAD

// Adahn PIDs

IF ~IsGabber(Player1) G("RJiniiJIDDK",9)~ THEN BEGIN ADDAK
SAY @300
IF ~~ THEN REPLY @301 DO ~AddJournalEntry(@9039,INFO)
                          AddexperienceParty(1500) 
                          SetGlobal("RJiniiJIDDK","GLOBAL",10)~ EXTERN RJPSTAD ADDAK1
END

IF ~IsGabber(Player1) G("RJiniiJIDAD",99)~ THEN BEGIN ADPID0
SAY @9
++ @10 EXIT
IF ~G("RJFinalVisit",1) G("RJADFV",0)~ THEN REPLY @400 GOTO ADPID9
IF ~G("RJAddRV",0)~ THEN REPLY @200 GOTO ADPID1
IF ~G("RJAddAN",0)~ THEN REPLY @201 GOTO ADPID2
IF ~G("RJAddGR",0)~ THEN REPLY @202 GOTO ADPID3
IF ~G("RJiniiJIDMR",99)~ THEN REPLY @220 GOTO ADPID4
IF ~G("RJiniiJIDND",99)~ THEN REPLY @221 GOTO ADPID5
IF ~G("RJiniiJIDDK",99)~ THEN REPLY @222 GOTO ADPID6
END

IF ~~ THEN BEGIN ADPID1
SAY @203 = @204 = @205 = @206
++ @207 EXIT
END

IF ~~ THEN BEGIN ADPID2
SAY @208 = @209 = @210 = @211
++ @212 EXIT
END

IF ~~ THEN BEGIN ADPID3
SAY @213 = @214 = @215
++ @216 EXIT
END

IF ~~ THEN BEGIN ADPID4
SAY @226
+ ~G("RJSYTalk",3) G("RJAddCnt",3)~ + @224 + ADPID4.1
+ ~G("RJSYTalk",3) !G("RJAddCnt",3)~ + @227 + ADPID4.2
+ ~G("RJSYTalk",2)~ + @223 + ADPID4.3
++ @235 EXIT
END

IF ~~ THEN BEGIN ADPID4.1
SAY @228
++ @229 DO ~SetGlobal("RJSYTalk","GLOBAL",4)~ EXIT
END

IF ~~ THEN BEGIN ADPID4.2
SAY @230
++ @231 DO ~SetGlobal("RJSYTalk","GLOBAL",4)~ EXIT
END

IF ~~ THEN BEGIN ADPID4.3
SAY @232 = @233
++ @234 EXIT
END

IF ~~ THEN BEGIN ADPID5
SAY @236 = @237
+ ~G("RJAddCnt",3)~ + @238 EXIT
+ ~!G("RJAddCnt",3)~ + @239 EXIT
END

IF ~~ THEN BEGIN ADPID6
SAY @240 = @241
++ @242 EXIT
END

IF ~~ THEN BEGIN ADPID9
SAY @401 = @402
++ @403 DO ~SetGlobal("RJADFV","GLOBAL",1)~ EXIT
END

IF ~IsGabber(Player1) !G("RJiniMet",2)~ THEN BEGIN ADPID10
SAY @350
IF ~~ THEN REPLY @351 EXIT
END

IF ~IsGabber(Player1) G("RJiniMet",2)~ THEN BEGIN ADPID11
SAY @352
IF ~~ THEN REPLY @353 EXIT
END

END
