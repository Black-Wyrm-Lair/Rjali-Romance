
//  Park NPCs in deArnise Keep

//  Petsy SoA

EXTEND_BOTTOM L3PETSYP 12
IF ~G("RjaliPetsyScout",2)
    G("L3PetsyRomanceActive",2)
    !G("KeepPlot",0)~ THEN REPLY @0 GOTO RJParkPetsySOA
END

APPEND L3PETSYP

IF ~~ THEN BEGIN RJParkPetsySOA
  SAY @22
  IF ~~ THEN DO ~SetGlobal("RJParkPE","GLOBAL",1)
                 IncrementGlobal("RJCntParked","GLOBAL",1)
                 EscapeAreaMove("AR1307",566,1059,0)~ EXIT
END

IF WEIGHT #-1 ~G("RJParkPE",1)~ THEN BEGIN RJVisitPetsySOA
  SAY @12
  IF ~~ THEN REPLY @13 EXIT
  IF ~~ THEN REPLY @7 DO ~
                 SetGlobal("RJParkPE","GLOBAL",0)
                 IncrementGlobal("RJCntParked","GLOBAL",-1)
                 JoinParty()~ EXIT
  IF ~RandomNum(3,1)~ THEN REPLY @9 GOTO RJVPESOA1
  IF ~RandomNum(3,2)~ THEN REPLY @9 GOTO RJVPESOA2
  IF ~RandomNum(3,3)~ THEN REPLY @9 GOTO RJVPESOA3
  IF ~~ THEN REPLY @11 GOTO RJVPESOA4
  IF ~~ THEN REPLY @700 GOTO RJVPESOA5
  IF ~~ THEN REPLY @8 GOTO RJVPESOA6
END

IF ~~ THEN BEGIN RJVPESOA1
  SAY @705 = @706 = @707
  IF ~~ THEN REPLY @708 EXIT
END

IF ~~ THEN BEGIN RJVPESOA2
  SAY @709 = @710
  IF ~~ THEN REPLY @711 EXIT
END

IF ~~ THEN BEGIN RJVPESOA3
  SAY @712 = @713
  IF ~~ THEN REPLY @714 EXIT
END

IF ~~ THEN BEGIN RJVPESOA4
  SAY @720 = @721
  IF ~~ THEN REPLY @722 EXIT
END

IF ~~ THEN BEGIN RJVPESOA5
  SAY @701 = @702 = @703
  IF ~~ THEN REPLY @704 EXIT
END

IF ~~ THEN BEGIN RJVPESOA6
  SAY @715 = @716 = @717 = @718
  IF ~~ THEN REPLY @719 EXIT
END

END

//   Petsy ToB

EXTEND_BOTTOM L3PET25P 24
IF ~G("L3PetsyRomanceActive",2)~ THEN REPLY @0 GOTO RJParkPetsyTOB
END

APPEND L3PET25P

IF ~~ THEN BEGIN RJParkPetsyTOB
  SAY @22
  IF ~~ THEN DO ~SetGlobal("RJParkPE","GLOBAL",1)
                 IncrementGlobal("RJCntParked","GLOBAL",1)
                 EscapeAreaMove("AR1307",566,1059,0)~ EXIT
END

IF WEIGHT #-1 ~G("RJParkPE",1)~ THEN BEGIN RJVisitPetsyTOB
  SAY @12
  IF ~G("RJFinalVisit",1) G("RJPEFV",0)~ THEN REPLY @14 GOTO RJVPETOB9
  IF ~~ THEN REPLY @13 EXIT
  IF ~G("RJPEFV",0)~ THEN REPLY @7 DO ~
                 SetGlobal("RJParkPE","GLOBAL",0)
                 IncrementGlobal("RJCntParked","GLOBAL",-1)
                 JoinParty()~ EXIT
  IF ~RandomNum(3,1)~ THEN REPLY @9 GOTO RJVPETOB1
  IF ~RandomNum(3,2)~ THEN REPLY @9 GOTO RJVPETOB2
  IF ~RandomNum(3,3)~ THEN REPLY @9 GOTO RJVPETOB3
  IF ~~ THEN REPLY @11 GOTO RJVPETOB4
  IF ~~ THEN REPLY @735 GOTO RJVPETOB5
  IF ~~ THEN REPLY @8 GOTO RJVPETOB6
END

IF ~~ THEN BEGIN RJVPETOB1
  SAY @748 = @749 = @750
  IF ~~ THEN REPLY @751 EXIT
END

IF ~~ THEN BEGIN RJVPETOB2
  SAY @752 = @753 = @754
  IF ~~ THEN REPLY @755 EXIT
END

IF ~~ THEN BEGIN RJVPETOB3
  SAY @756 = @757 = @758 = @759
  IF ~~ THEN REPLY @760 EXIT
END

IF ~~ THEN BEGIN RJVPETOB4
  SAY @745 = @746
  IF ~~ THEN REPLY @747 EXIT
END

IF ~~ THEN BEGIN RJVPETOB5
  SAY @736 = @737 = @738
  IF ~~ THEN REPLY @739 EXIT
END

IF ~~ THEN BEGIN RJVPETOB6
  SAY @740 = @741 = @742 = @743
  IF ~~ THEN REPLY @744 EXIT
END

IF ~~ THEN BEGIN RJVPETOB9
  SAY @730
  IF ~~ THEN REPLY @731 GOTO RJVPETOB9.1
END

IF ~~ THEN BEGIN RJVPETOB9.1
  SAY @732 = @733
  IF ~~ THEN REPLY @734 DO ~SetGlobal("RJPEFV","GLOBAL",1)~ EXIT
END

END
