
// This is where Kesai's romance talks start.

IF
InParty(Myself)
G("RJKesaiTalkToB",0)
THEN
RESPONSE #100
RealSetGlobalTimer("RJKesaiRomance","GLOBAL",2200)
SetGlobal("RJKesaiTalkToB","GLOBAL",1)
END

// This block sets up romance talks

IF
InParty(Myself)
RealGlobalTimerExpired("RJKesaiRomance","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
OR(6)
G("RJKesaiTalkToB",1)
G("RJKesaiTalkToB",3)
G("RJKesaiTalkToB",5)
G("RJKesaiTalkToB",7)
G("RJKesaiTalkToB",9)
G("RJKesaiTalkToB",11)
THEN
RESPONSE #100
IncrementGlobal("RJKesaiTalkToB","GLOBAL",1)
END

// This block initiates a dialog with Player1

IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(6)
  G("RJKesaiTalkToB",2)
  G("RJKesaiTalkToB",4)
  G("RJKesaiTalkToB",6)
  G("RJKesaiTalkToB",8)
  G("RJKesaiTalkToB",10)
  G("RJKesaiTalkToB",12)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Kesai initiated flirts

// Flirts initiate

IF
InParty(Myself)
G("RJKesaiFlirtsStart",1)
THEN
RESPONSE #100
SetGlobal("RJKesaiFlirtsStart","GLOBAL",2)
SetGlobal("RJKesaiFlirt","GLOBAL",0)
RealSetGlobalTimer("RJKesaiFlirtTimer","GLOBAL",RJ_FAST)
END

// Ready for flirting

IF
InParty(Myself)
G("RJHush",0)
G("RJKesaiFlirtsStart",2)
RealGlobalTimerExpired("RJKesaiFlirtTimer","GLOBAL")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
G("RJKesaiFlirt",0)
THEN
RESPONSE #100
SetGlobal("RJKesaiFlirt","GLOBAL",1)
StartDialogueNoSet(Player1)
END

// Flirting begins

IF
InParty(Myself)
G("RJKesaiFlirt",1)
RealGlobalTimerExpired("RJKesaiFlirtTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("RJKesaiFlirt","GLOBAL",0)
RealSetGlobalTimer("RJKesaiFlirtTimer","GLOBAL",RJ_FLIRT)
END

IF
G("RJKesaiFlirtsStart",2)
G("RJHush",0)
RealGlobalTimerExpired("RJKesaiFlirtTimer","GLOBAL")
!CombatCounter(0)
THEN
RESPONSE #100
RealSetGlobalTimer("RJKesaiFlirtTimer","GLOBAL",150)
END
