
// This is where Grace's romance talks start.

IF
InParty(Myself)
G("RJGraceTalkToB",0)
THEN
RESPONSE #100
RealSetGlobalTimer("RJGraceRomance","GLOBAL",2600)
SetGlobal("RJGraceTalkToB","GLOBAL",1)
END

// This block sets up romance talks

IF
InParty(Myself)
RealGlobalTimerExpired("RJGraceRomance","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
OR(6)
G("RJGraceTalkToB",1)
G("RJGraceTalkToB",3)
G("RJGraceTalkToB",5)
G("RJGraceTalkToB",7)
G("RJGraceTalkToB",9)
G("RJGraceTalkToB",11)
THEN
RESPONSE #100
IncrementGlobal("RJGraceTalkToB","GLOBAL",1)
END

// This block initiates a dialog with Player1

IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(6)
  G("RJGraceTalkToB",2)
  G("RJGraceTalkToB",4)
  G("RJGraceTalkToB",6)
  G("RJGraceTalkToB",8)
  G("RJGraceTalkToB",10)
  G("RJGraceTalkToB",12)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Grace initiated flirts

// Flirts initiate

IF
InParty(Myself)
G("RJGraceFlirtsStart",1)
THEN
RESPONSE #100
SetGlobal("RJGraceFlirtsStart","GLOBAL",2)
RealSetGlobalTimer("RJGraceFlirtTimer","GLOBAL",RJ_FAST)
END

// Ready for flirting

IF
InParty(Myself)
G("RJHush",0)
G("RJGraceFlirtsStart",2)
RealGlobalTimerExpired("RJGraceFlirtTimer","GLOBAL")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
G("RJGraceFlirt",0)
THEN
RESPONSE #100
SetGlobal("RJGraceFlirt","GLOBAL",1)
StartDialogueNoSet(Player1)
END

// Flirting begins

IF
InParty(Myself)
G("RJGraceFlirt",1)
RealGlobalTimerExpired("RJGraceFlirtTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("RJGraceFlirt","GLOBAL",0)
RealSetGlobalTimer("RJGraceFlirtTimer","GLOBAL",RJ_FLIRT)
END

IF
G("RJGraceFlirtsStart",2)
G("RJHush",0)
RealGlobalTimerExpired("RJGraceFlirtTimer","GLOBAL")
!CombatCounter(0)
THEN
RESPONSE #100
RealSetGlobalTimer("RJGraceFlirtTimer","GLOBAL",150)
END
