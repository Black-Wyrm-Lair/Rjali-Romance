
// Bump XP on initial join

IF
InParty("RJRavel")
Global("RJRavelSetXP","GLOBAL",0)
XPLT(Player1,280001)
THEN
RESPONSE #100
SetGlobal("RJRavelSetXP","GLOBAL",1)
Continue()
END

IF
InParty("RJRavel")
Global("RJRavelSetXP","GLOBAL",0)
XPGT(Player1,280000)
XPLT(Player1,600001)
THEN
RESPONSE #100
SetGlobal("RJRavelSetXP","GLOBAL",1)
AddXPObject("RJRavel",411000)
Continue()
END

IF
InParty("RJRavel")
Global("RJRavelSetXP","GLOBAL",0)
XPGT(Player1,600000)
XPLT(Player1,1000001)
THEN
RESPONSE #100
SetGlobal("RJRavelSetXP","GLOBAL",1)
AddXPObject("RJRavel",711000)
Continue()
END

IF
InParty("RJRavel")
Global("RJRavelSetXP","GLOBAL",0)
XPGT(Player1,1000000)
THEN
RESPONSE #100
SetGlobal("RJRavelSetXP","GLOBAL",1)
AddXPObject("RJRavel",1111000)
Continue()
END

// This is where Ravel's romance talks start.

IF
GGT("Chapter",%bg2_chapter_1%)
InParty(Myself)
G("RJRavelTalk",0)
THEN
RESPONSE #100
RealSetGlobalTimer("RJRavelRomance","GLOBAL",800)
SetGlobal("RJRavelTalk","GLOBAL",1)
END

// This block sets up romance talks

IF
InParty(Myself)
RealGlobalTimerExpired("RJRavelRomance","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
OR(12)
G("RJRavelTalk",1)
G("RJRavelTalk",3)
G("RJRavelTalk",5)
G("RJRavelTalk",9)
G("RJRavelTalk",11)
G("RJRavelTalk",13)
G("RJRavelTalk",15)
G("RJRavelTalk",17)
G("RJRavelTalk",19)
G("RJRavelTalk",21)
G("RJRavelTalk",23)
G("RJRavelTalk",25)
THEN
RESPONSE #100
IncrementGlobal("RJRavelTalk","GLOBAL",1)
END

// This block sets up romance talk 4

IF
InParty(Myself)
PartyRested()
RealGlobalTimerExpired("RJRavelRomance","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
G("RJRavelTalk",7)
THEN
RESPONSE #100
IncrementGlobal("RJRavelTalk","GLOBAL",1)
END

// This block sets up romance talk 14

IF
InParty(Myself)
G("RJRavelOrphans",0)
RealGlobalTimerExpired("RJRavelRomance","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
G("RJRavelTalk",27)
!G("RjaliMarried",0)
THEN
RESPONSE #100
IncrementGlobal("RJRavelTalk","GLOBAL",1)
END

// This block initiates a dialog with Player1

IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(15)
  G("RJRavelTalk",2)
  G("RJRavelTalk",4)
  G("RJRavelTalk",6)
  G("RJRavelTalk",8)
  G("RJRavelTalk",10)
  G("RJRavelTalk",12)
  G("RJRavelTalk",14)
  G("RJRavelTalk",16)
  G("RJRavelTalk",18)
  G("RJRavelTalk",20)
  G("RJRavelTalk",22)
  G("RJRavelTalk",24)
  G("RJRavelTalk",26)
  G("RJRavelTalk",28)
  G("RJRavelSleptPhaere",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Phaere Innuendo

IF
  InParty(Myself)
  !Range("Phaere",6)
  G("RJRavelSleptPhaere",0)
  AreaCheck("AR2204")
  See(Player1)
  OR(2)
    G("PhaereInnuendo",2)
    G("PhaereInnuendo",3)
THEN
  RESPONSE #100
    SetGlobal("RJRavelSleptPhaere","GLOBAL",1)
END

// Ravel's post Ust Natha inn sleep

IF
  InParty(Myself)
  G("RJRVUDSleep1",2)
  PartyRested()
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJRVUDSleep1","GLOBAL",3)
END

IF
  InParty(Myself)
  G("RJRVUDSleep1",3)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

/* Ravel familiar test

IF
InParty(Myself)
G("RJRavelFamiliar",0)
THEN
RESPONSE #100
CreateCreatureObject("RJPSTKE",Myself,0,0,0)
ActionOverride("RJKesai",MakeGlobal())
ActionOverride("RJKesai",ChangeEnemyAlly(Myself,FAMILIAR))
ActionOverride("RJKesai",AddFamiliar())
SetGlobal("RJRavelFamiliar","GLOBAL",1)
END

*/

// Ravel initiated flirts

// Flirts initiate

IF
InParty(Myself)
G("RJRavelFlirtsStart",0)
THEN
RESPONSE #100
SetGlobal("RJRavelFlirtsStart","GLOBAL",1)
RealSetGlobalTimer("RJRavelFlirtTimer","GLOBAL",RJ_FAST)
END

// Ready for flirting

IF
InParty(Myself)
G("RJHush",0)
G("RJRavelFlirtsStart",1)
RealGlobalTimerExpired("RJRavelFlirtTimer","GLOBAL")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
G("RJRavelFlirt",0)
THEN
RESPONSE #100
SetGlobal("RJRavelFlirt","GLOBAL",1)
StartDialogueNoSet(Player1)
END

// Flirting begins

IF
InParty(Myself)
G("RJRavelFlirt",1)
G("RJRavelRomanceActive",1)
RealGlobalTimerExpired("RJRavelFlirtTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("RJRavelFlirt","GLOBAL",0)
RealSetGlobalTimer("RJRavelFlirtTimer","GLOBAL",RJ_FAST)
END

IF
InParty(Myself)
G("RJRavelFlirt",1)
G("RJRavelRomanceActive",2)
RealGlobalTimerExpired("RJRavelFlirtTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("RJRavelFlirt","GLOBAL",0)
RealSetGlobalTimer("RJRavelFlirtTimer","GLOBAL",RJ_FLIRT)
END

IF
G("RJRavelFlirtsStart",1)
G("RJHush",0)
RealGlobalTimerExpired("RJRavelFlirtTimer","GLOBAL")
!CombatCounter(0)
THEN
RESPONSE #100
RealSetGlobalTimer("RJRavelFlirtTimer","GLOBAL",150)
END
