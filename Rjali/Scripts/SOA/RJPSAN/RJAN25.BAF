
// This is where Annah's romance talks start.

IF
InParty(Myself)
G("RJAnnahTalkToB",0)
THEN
RESPONSE #100
RealSetGlobalTimer("RJAnnahRomance","GLOBAL",1800)
SetGlobal("RJAnnahTalkToB","GLOBAL",1)
END

// This block sets up romance talks

IF
InParty(Myself)
RealGlobalTimerExpired("RJAnnahRomance","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
OR(6)
G("RJAnnahTalkToB",1)
G("RJAnnahTalkToB",3)
G("RJAnnahTalkToB",5)
G("RJAnnahTalkToB",7)
G("RJAnnahTalkToB",9)
G("RJAnnahTalkToB",11)
THEN
RESPONSE #100
IncrementGlobal("RJAnnahTalkToB","GLOBAL",1)
END

// This block initiates a dialog with Player1

IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(6)
  G("RJAnnahTalkToB",2)
  G("RJAnnahTalkToB",4)
  G("RJAnnahTalkToB",6)
  G("RJAnnahTalkToB",8)
  G("RJAnnahTalkToB",10)
  G("RJAnnahTalkToB",12)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Annah initiated flirts

// Flirts initiate

IF
InParty(Myself)
G("RJAnnahFlirtsStart",1)
THEN
RESPONSE #100
SetGlobal("RJAnnahFlirtsStart","GLOBAL",2)
RealSetGlobalTimer("RJAnnahFlirtTimer","GLOBAL",RJ_FAST)
END

// Ready for flirting

IF
InParty(Myself)
G("RJHush",0)
G("RJAnnahFlirtsStart",2)
RealGlobalTimerExpired("RJAnnahFlirtTimer","GLOBAL")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
G("RJAnnahFlirt",0)
THEN
RESPONSE #100
SetGlobal("RJAnnahFlirt","GLOBAL",1)
StartDialogueNoSet(Player1)
END

// Flirting begins

IF
InParty(Myself)
G("RJAnnahFlirt",1)
RealGlobalTimerExpired("RJAnnahFlirtTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("RJAnnahFlirt","GLOBAL",0)
RealSetGlobalTimer("RJAnnahFlirtTimer","GLOBAL",RJ_FLIRT)
END

IF
G("RJAnnahFlirtsStart",2)
G("RJHush",0)
RealGlobalTimerExpired("RJAnnahFlirtTimer","GLOBAL")
!CombatCounter(0)
THEN
RESPONSE #100
RealSetGlobalTimer("RJAnnahFlirtTimer","GLOBAL",150)
END
